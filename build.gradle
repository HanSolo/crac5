import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'application'
    id 'net.nemerosa.versioning' version '3.0.0'
}

description = 'CRaC 5 Demo'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM.dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

application {
    mainModule = 'eu.hansolo.crac5'
    mainClass  = 'eu.hansolo.crac5.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.crac:crac:1.5.0'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
    manifest {
        attributes(
                'Build-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + "  " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Main-Class'            : application.mainClass.get()
        )
    }
    // Create fat jar to make sure org.crac is available
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

if (JavaVersion.current().isJava8Compatible()){
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task Main (type: JavaExec) {
    mainClass = "eu.hansolo.crac5.Main"
    classpath = sourceSets.main.runtimeClasspath
}